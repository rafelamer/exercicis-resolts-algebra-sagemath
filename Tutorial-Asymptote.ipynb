{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    "%load_ext asymagic"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%asy --fmt svg\n",
    "\n",
    "unitsize(1cm);\n",
    "pair A=0, B=(5,0), C=(7.5,7.5);\n",
    " \n",
    "draw(A--B--C--cycle);\n",
    "dot(\"$A$\",A,dir(C--A,B--A));  \n",
    "dot(\"$B$\",B,dir(C--B,A--B));  \n",
    "dot(\"$C$\",C,dir(A--C,B--C));"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%asy --fmt svg\n",
    "\n",
    "size(6cm,0);\n",
    "\n",
    "pair A=0, B=(3,0), C=(2.5,3);\n",
    "path p=A--B--C--cycle;\n",
    "draw(p);\n",
    "\n",
    "pair[] m=new pair[2];\n",
    "\n",
    "for (int i = 0; i <= 2; ++i) {\n",
    "  m[i]=(point(p,i+1)+point(p,i+2))/2;\n",
    "  draw (point(p,i)--m[i]);  \n",
    "}\n",
    "\n",
    "dot(\"$G$\",(A+B+C)/3,2E,red+6bp);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%asy --fmt svg\n",
    "\n",
    "import geometry;\n",
    "\n",
    "size(7.5cm,0);\n",
    "\n",
    "point pA=(1,1),pB=(5,0),pC=(1.5,6);\n",
    "triangle t=triangle(pA,pB,pC);\n",
    "draw(t,.8bp+red);\n",
    "label(t);\n",
    " \n",
    "draw(circumcircle(t));\n",
    "dot(\"$\\Omega$\",circumcenter(t));\n",
    "\n",
    "addMargins(.5cm,.5cm);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%asy --fmt svg\n",
    "\n",
    "size(6cm,0);\n",
    "\n",
    "path s1=(-1,-1)--(5,2),\n",
    "     s2=(0,3)--(2,-3);\n",
    "draw(s1^^s2,blue);\n",
    "\n",
    "dot(intersectionpoint(s1,s2),4bp+red);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%asy --fmt svg\n",
    "\n",
    "size(7.5cm,0);\n",
    "\n",
    "pair p1=(-5,5), p2=(-1,1);\n",
    "path r=p1--p2,\n",
    "     c=circle((1,0),3);\n",
    "pair pA=intersectionpoint(r,c),\n",
    "     pB=intersectionpoint(p2--interp(p1,p2,2),c);\n",
    "    \n",
    "draw(r,blue);\n",
    "draw(c,orange);\n",
    "dot(\"$A$\",pA,2W,4bp+brown);\n",
    "dot(\"$B$\",pB,2SE,4bp+black);\n",
    "draw(p2--pB,dashed+blue);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%asy --fmt svg\n",
    "\n",
    "size(7.5cm,0);\n",
    "\n",
    "path c;\n",
    "for (int i=0; i<=7; ++i) \n",
    "  c=c--point(polygon(7),2*i);\n",
    "pair[] p = intersectionpoints(c,c);\n",
    "\n",
    "draw(polygon(7),2bp+green);\n",
    "draw(c);\n",
    "for (int k=0; k<p.length; ++k) \n",
    "  dot(format(\"%i\",k),p[k],3N,5bp+red);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%asy --fmt svg\n",
    "\n",
    "size(6cm,0);\n",
    "\n",
    "draw(unitcircle);\n",
    "dot((0,0));\n",
    "draw(Label(\"$\\vec r$\"),(0,0)--dir(45),Arrow);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%asy --fmt svg\n",
    "\n",
    "size(7cm,0);\n",
    "\n",
    "draw(unitcircle,green);\n",
    "draw(scale(2)*unitcircle,red);\n",
    "draw(shift(1,0)*unitcircle,blue);\n",
    "draw(shift(1,0)*scale(2)*unitcircle,2bp+orange);\n",
    "dot(\"O\",(0,0));"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%asy --fmt svg\n",
    "\n",
    "import geometry;\n",
    "\n",
    "size(12.5cm,0);\n",
    "\n",
    "point foyer=(0,0); \n",
    "dot(\"F\", foyer, W);\n",
    "\n",
    "line directrice=line((1,-1),(1,1)); \n",
    "draw(directrice,dashed+.7bp+green);\n",
    "\n",
    "conic ell=conic(foyer,directrice,0.8); \n",
    "draw(ell,1.2bp+blue);\n",
    "\n",
    "conic par=conic(foyer,directrice,1); \n",
    "draw(par,1.2bp+red);\n",
    "\n",
    "draw(box((-3,-4), (3,4)), invisible);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%asy --fmt svg\n",
    "\n",
    "import geometry;\n",
    "\n",
    "size(10cm,0);\n",
    "\n",
    "point pA=(4,0), pB=(0,2), pC=(-4,0), pD=(0,-2), pE=(3,1);\n",
    "dot(pA^^pB^^pC^^pD^^pE);\n",
    "\n",
    "conic c = conic(pA,pB,pC,pD,pE);\n",
    "draw(c,1.3bp+.8green);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%asy --fmt pdf\n",
    "\n",
    "settings.render = 10;\n",
    "settings.prc = false;\n",
    "\n",
    "import graph3;\n",
    "size(8cm,0);\n",
    "\n",
    "currentprojection=perspective(5,5,3.5);\n",
    "currentlight=light(white,(2,2,2),(2,-2,-2));\n",
    "\n",
    "path3 p=(-1,-1,0)--(1,-1,0)--(1,1,0)--(-1,1,0)--cycle;\n",
    "draw(p,paleblue);\n",
    "\n",
    "path3 g=(1,0,0)..(0,1,1)..(-1,0,0)..(0,-1,1)..cycle;\n",
    "draw(g,2bp+.8red);\n",
    "draw(surface(g),opacity(0.8)+paleblue);\n",
    "dot(g,5bp+.8green);\n",
    "\n",
    "limits((-1.5,-1.5,0),(1.4,1.4,1.5));\n",
    "xaxis3(\"$x$\",Arrow3); \n",
    "yaxis3(\"$y$\",Arrow3);\n",
    "zaxis3(\"$z$\",Arrow3);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%asy --fmt pdf\n",
    "\n",
    "settings.render = 10;\n",
    "settings.prc = false;\n",
    "\n",
    "import three;\n",
    "\n",
    "currentprojection=perspective((10,8,4));\n",
    "unitsize(0.6666cm);\n",
    "\n",
    "/*\n",
    "    L'equció del pla pl és z=0 i la del pla pl2 és 13y+16z-8=0\n",
    "*/\n",
    "path3 pl = (-7,-11,0)--(7,-11,0)--(7,11,0)--(-7,11,0)--cycle;\n",
    "path3 pl2 = (-7,-8,7)--(7,-8,7)--(7,8,-6)--(-7,8,-6)--cycle;\n",
    "\n",
    "/*\n",
    "    Els punts P0 i P1 són del pla pl i els punts Q0 i P1 són de pl2\n",
    "*/\n",
    "triple P0 = (5,-9,0),\n",
    "       P1 = (5,9,0),\n",
    "       Q0 = (-4.0,-7,6),\n",
    "       Q1 = (-4.0,7,-5.1875);\n",
    "draw(P0--P1,1.2bp+black);\n",
    "draw(Q0--Q1,1.2bp+black);\n",
    "       \n",
    "/*\n",
    "    La recta intersecció dels dos plans és\n",
    "             y=0.615385\n",
    "             z=0\n",
    "    i els punts R0 i R1 són d'aquesta recta. A més,\n",
    "    aquesta recta és perpendicular a les rectes POP1\n",
    "    a Q0Q1\n",
    "*/\n",
    "triple R0 = (-8.5,0.615385,0),\n",
    "       R1 = (8.5,0.615385,0);\n",
    "draw(R0--R1,1.4bp+red);    \n",
    "draw(surface(pl2),lightgreen+opacity(0.2),1.2bp+.8green);\n",
    "draw(surface(pl),lightblue+opacity(0.2),1.2bp+.8blue);\n",
    "label(\"$P$\",(-7,11,0.45),blue);    \n",
    "\n",
    "/*\n",
    "    La intersecció de la recta P0P1 amb el pla pl2 és R0 i el de\n",
    "    la recta Q0Q1 amb el pla pl és R1\n",
    "*/\n",
    "triple R2 = (5,0.615385,0),\n",
    "       R3 = (-4,0.615385,0);\n",
    "dot(R2,5bp+red);\n",
    "dot(R3,5bp+red);\n",
    "label(\"$Q$\",(7,-8,7.45),green);\n",
    "/*\n",
    "    Dos punts de la recta POP1\n",
    "*/\n",
    "triple P2 = (5,3,0),\n",
    "       P3 = (5,5.5,0);\n",
    "dot(P2,4.25bp+black);\n",
    "draw(P2--P3,2bp+black,Arrow3(6));\n",
    "label(\"$p_0$\",P2,Z);\n",
    "label(\"$\\vec u$\",(P2+P3)/2,Z);\n",
    "\n",
    "/*\n",
    "    La recta Q0Q1 té equació \n",
    "          x=-4\n",
    "          13y+16z-8=0\n",
    "    i dos punts d'aquesta recta són Q2 i Q3\n",
    "    Els -3.875 haurien de ser 4.0, però amb el 4, el gràfic no surt be\n",
    "    Pot ser és un bug de l'asy\n",
    "*/\n",
    "triple Q2 = (-3.875,-1,1.3125),\n",
    "       Q3 = (-3.875,-3,2.9375);\n",
    "dot(Q2,4.25bp+black);\n",
    "draw(Q2--Q3,2bp+black,Arrow3(6));\n",
    "label(\"$q_0$\",Q2,-1.1X);\n",
    "label(\"$\\vec v$\",(Q2+Q3)/2,-1.7X);\n",
    "/*\n",
    "    Dos punts de la perpendicular comuna\n",
    "*/\n",
    "triple R4 = (-1,0.615385,0),\n",
    "       R5 = (2,0.615385,0);\n",
    "dot(R4,4.25bp+red);      \n",
    "draw(R4--R5,2bp+red,Arrow3(7));\n",
    "label(\"$\\vec u\\times\\vec v$\",(R4+R5)/2,X+Z,red);    \n",
    "\n",
    "/*\n",
    "    Rectes R i R'\n",
    "*/\n",
    "label(\"$R$\",P1,1.2Y,black);\n",
    "label(\"$R'$\",Q0,1.2X,black);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%asy --fmt pdf\n",
    "\n",
    "settings.render = 10;\n",
    "settings.prc = false;\n",
    "\n",
    "import solids;\n",
    "import graph3;\n",
    "\n",
    "size(16cm,0);\n",
    "currentprojection=orthographic(16,9,3.5);\n",
    "\n",
    "real a = 1.35,\n",
    "     t = 5.1,\n",
    "     h = 0.0,\n",
    "     m = 6.2,\n",
    "     alpha = radians(1);\n",
    "     \n",
    "limits((0,0,0),(20,20,20));\n",
    "\n",
    "real x(real t) { return 0.0; }\n",
    "real y(real t) { return t; }\n",
    "real z(real t) { return a * t; }\n",
    "h = z(t);\n",
    "\n",
    "draw((-12,0,0)--(12,0,0),red+linewidth(0.3mm),Arrow3(5));\n",
    "draw((0,-12,0)--(0,12,0),red+linewidth(0.3mm),Arrow3(5));\n",
    "draw((0,0,-12)--(0,0,12),red+linewidth(0.3mm),Arrow3(5));\n",
    "\n",
    "draw(graph(x,y,z,-m,m,operator ..),black+linewidth(0.3mm));\n",
    "\n",
    "path3 g = (0,t,h)..(t,0,h)..(0,-t,h)..(-t,0,h)..cycle;\n",
    "draw(g,blue+linetype(\"4 4 4 4\")+linewidth(0.3mm));\n",
    "\n",
    "triple O = (0,0,h),\n",
    "       P = (0,t,h),\n",
    "       Q = (t * cos(alpha),t * sin(alpha),h);\n",
    "\n",
    "dot(O,5bp+blue);\n",
    "dot(P,5bp+black);\n",
    "dot(Q,5bp+blue);\n",
    "\n",
    "draw(P--O--Q,blue+linetype(\"0 4 4 4\")+linewidth(0.3mm));\n",
    "\n",
    "label(\"$p_0=(0,0,s)$\",O,Z);\n",
    "label(\"$p=(0,t,s)$\",P,X+2.5Y-0.8Z);\n",
    "label(\"$q=(x,y,z)$\",Q,-X+Y-1.4Z);\n",
    "\n",
    "revolution con = revolution(graph(new triple(real t) {\n",
    "     return (0,t,a * t);},\n",
    "      -m,m,25,operator ..),axis=Z);\n",
    "draw(surface(con),opacity(0.3)+lightgreen);\n",
    "\n",
    "label(\"$z$\",(0,0,12.5),red);\n",
    "label(\"$x$\",(12.5,0,0),red);\n",
    "label(\"$y$\",(0,12.5,0),red);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%asy --fmt svg\n",
    "\n",
    "import coordenades;\n",
    "\n",
    "unitsize(1cm);\n",
    "Canonica(-12,12,-7,7);\n",
    "real f(real x) {return 0.5 * x * sin(x);}\n",
    "path g = graph(f,-11,11,n=200);\n",
    "draw(g,1bp+red);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%asy --fmt svg\n",
    "import geometry;\n",
    "import coordenades;\n",
    "import graph;\n",
    "\n",
    "unitsize(1cm);\n",
    "\n",
    "Canonica(-10,10,-8,14);\n",
    "Parabola((-2,3),(1,5),x=9,y=9);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%asy --fmt svg\n",
    "import geometry;\n",
    "import coordenades;\n",
    "import graph;\n",
    "\n",
    "unitsize(1cm);\n",
    "\n",
    "Canonica(-10,12,-10,9);\n",
    "Hiperbola((1,-1),(3,2),9,3,x=10,y=10);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%asy --fmt svg\n",
    "import geometry;\n",
    "import coordenades;\n",
    "import graph;\n",
    "\n",
    "unitsize(1cm);\n",
    "\n",
    "Canonica(-7,13,-8,11);\n",
    "Elipse((3,2),(1,3),40,20,x=9,y=9);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%asy --fmt svg\n",
    "\n",
    "import coordenades;\n",
    "\n",
    "unitsize(1cm);\n",
    "add(PiCoordenades(-5,5,-1,1,sizey=3));\n",
    "\n",
    "real f(real x) {return sin(x);}\n",
    "path Cf=graph(f,-5*pi,5*pi,n=500);\n",
    "draw(scale(1,3)*Cf,linewidth(0.55mm)+blue);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%asy --fmt svg\n",
    "\n",
    "import coordenades;\n",
    "\n",
    "unitsize(1cm);\n",
    "\n",
    "Canonica(-14,12,-8,14);\n",
    "\n",
    "pen lx = linetype(\"8 8 8 8\")+linewidth(0.1mm)+gray(0.5),\n",
    "    ly = linewidth(0.4mm)+black;\n",
    "pair p = (5,8),\n",
    "     px = (p.x,0),\n",
    "     py = (0,p.y);\n",
    "\n",
    "draw(px--p--py,lx);\n",
    "draw((0,0)--p,ly,Arrow(8));\n",
    "draw((0,0)--(-2,3),ly,Arrow(8));\n",
    "\n",
    "picture r = Coordenades(-5,5,-9,9,color=red,base=true,quadricula=false,ticky=0.1);\n",
    "transform tr = (-2,3,1,-1,2,1);\n",
    "\n",
    "pair pp = (4,-3),\n",
    "    ppx = (pp.x,0),\n",
    "    ppy = (0,pp.y);\n",
    "lx =  linetype(\"8 8 8 8\")+linewidth(0.1mm)+red;\n",
    "ly = linewidth(0.4mm)+gray(0.6);\n",
    "draw(r,ppx--pp--ppy,lx);\n",
    "draw(r,(0,0)--pp,ly,Arrow(8));\n",
    "\n",
    "add(tr * r);\n",
    "\n",
    "dot(\"$p$\",p,6bp+black);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 10.1",
   "language": "sage",
   "name": "sagemath"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.11.8"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
